module skidbuffer
#(
    parameter type DTYPE = logic
)
(
    input  logic                  clk,
    input  logic                  rstn,

    input  logic                  input_valid,
    output logic                  input_ready,
    input  DTYPE                  input_data,

    output logic                  output_valid,
    input  logic                  output_ready,
    output DTYPE                  output_data,

    output logic                  empty
);
    DTYPE selected_data;
    DTYPE input_buffer_out;
    logic input_buffer_ce, output_buffer_ce, use_buffered_data;
    logic load, flow, fill, flush, unload;
    logic insert, remove;

    typedef enum logic [1:0] {
        eEMPTY, // Output and buffer registers empty
        eBUSY,  // Output register holds data
        eFULL   // Both output and buffer registers full
    } sb_fsm_e;
    sb_fsm_e state, state_next;

    assign empty = (state == eEMPTY);

    /*************************************
    * Data Path
    *************************************/
    register #(
        .DTYPE (DTYPE),
        .RESET_VALUE (0)
    ) input_buffer (
        .clk  (clk),
        .rstn (rstn),
        .ce   (input_buffer_ce),
        .in   (input_data),
        .out  (input_buffer_out)
    );
    assign selected_data = use_buffered_data ? input_buffer_out : input_data;
    register #(
        .DTYPE (DTYPE),
        .RESET_VALUE (0)
    ) output_buffer (
        .clk  (clk),
        .rstn (rstn && ~unload),
        .ce   (output_buffer_ce),
        .in   (selected_data),
        .out  (output_data)
    );

    /*************************************
    * Control Logic
    *************************************/
    register #(
        .DTYPE (logic),
        .RESET_VALUE (1'b1)
    ) input_ready_reg (
        .clk  (clk),
        .rstn (rstn),
        .ce   (1'b1),
        .in   ((state_next != eFULL)),
        .out  (input_ready)
    );
    register #(
        .DTYPE (logic),
        .RESET_VALUE (1'b0)
    ) output_valid_reg (
        .clk  (clk),
        .rstn (rstn),
        .ce   (1'b1),
        .in   (state_next != eEMPTY),
        .out  (output_valid)
    );
    assign insert = input_valid  && input_ready;
    assign remove = output_valid && output_ready;
    always_comb begin
        load    = (state == eEMPTY) &&  insert && ~remove;
        flow    = (state == eBUSY)  &&  insert &&  remove;
        fill    = (state == eBUSY)  &&  insert && ~remove;
        unload  = (state == eBUSY)  && ~insert &&  remove;
        flush   = (state == eFULL)  && ~insert &&  remove;
    end

    assign input_buffer_ce   = fill;
    assign output_buffer_ce  = load || flow || flush;
    assign use_buffered_data = flush;

    always_comb begin
        state_next = load   ? eBUSY  : state;
        state_next = flow   ? eBUSY  : state_next;
        state_next = fill   ? eFULL  : state_next;
        state_next = flush  ? eBUSY  : state_next;
        state_next = unload ? eEMPTY : state_next;
    end
    register #(
        .DTYPE (sb_fsm_e),
        .RESET_VALUE (eEMPTY)
    ) state_reg (
        .clk  (clk),
        .rstn (rstn),
        .ce   (1'b1),
        .in   (state_next),
        .out  (state)
    );
endmodule
